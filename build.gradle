plugins {
    id 'java'
    id "io.qameta.allure" version "2.8.1"
}

group 'template.ekam'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "http://palantir.bintray.com/releases" }
}

dependencies {
    implementation('com.testvagrant.ekam:ekam:0.0.1-alpha')

    // Use TestNG framework, also requires calling test.useTestNG() below
    implementation('org.testng:testng:6.14.3')

    // cucumber
    implementation('io.cucumber:cucumber-java:4.8.1')
    implementation('io.cucumber:cucumber-testng:4.8.1')

    //google
    implementation('com.google.guava:guava:28.0-jre')
    implementation('com.google.inject:guice:4.2.3')
    implementation("com.google.protobuf:protobuf-java:3.9.1")

    // retrofit
    implementation('com.squareup.retrofit2:retrofit:2.9.0')
    implementation('com.squareup.retrofit2:converter-gson:2.9.0')

    //okhttp3
    implementation('com.squareup.okhttp3:logging-interceptor:3.8.0')

    //grpc
    implementation('io.grpc:grpc-all:1.19.0')

    //lombok
    implementation('org.projectlombok:lombok:1.18.12')
    annotationProcessor('org.projectlombok:lombok:1.18.12')
    testImplementation('org.projectlombok:lombok:1.18.12')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.12')

    //jboss
    implementation('org.jboss.aerogear:aerogear-otp-java:1.0.0')

    //allure
    implementation('io.qameta.allure:allure-gradle:2.8.1')
    implementation('io.qameta.allure:allure-java-commons:2.13.2')
    implementation('io.qameta.allure:allure-testng:2.13.2')
    implementation('io.qameta.allure:allure-okhttp3:2.10.0')
    implementation('io.qameta.allure:allure-rest-assured:2.10.0')

    //logger
    implementation('org.apache.logging.log4j:log4j-api:2.8')
    implementation('org.apache.logging.log4j:log4j-core:2.8')
    implementation('org.apache.logging.log4j:log4j-slf4j-impl:2.13.3')

    //palantir
    implementation('com.palantir.roboslack:roboslack-webhook-api:1.3.0')
    implementation('com.palantir.roboslack:roboslack-api:1.3.0')
    implementation('com.palantir.roboslack:roboslack-webhook:1.3.0')

    //github
    implementation('com.github.javafaker:javafaker:1.0.2')

    //appium
    implementation('io.appium:java-client:7.5.1')

    //selenium
    implementation('org.seleniumhq.selenium:selenium-java:3.141.59')
    implementation('io.github.bonigarcia:webdrivermanager:4.0.0')

    implementation('org.postgresql:postgresql:42.2.1')
    implementation('commons-dbutils:commons-dbutils:1.7')
    implementation('org.jeasy:easy-random:4.2.0')
    implementation('org.jdbi:jdbi:2.78')
    implementation("javax.annotation:javax.annotation-api:1.3.2")

    // generic wait
    implementation('org.awaitility:awaitility:4.0.3')
}


tasks.withType(Test) {
    systemProperties = [
            apiConfig             : System.getProperty('apiConfig', 'apiConfig'),
            dbConfig              : System.getProperty('dbConfig', 'dbConfig'),
            env                   : System.getProperty('env'),

            tags                  : System.getProperty('tags', 'smoke'),
            logs                  : System.getProperty('logs', 'false'),
            timeline              : System.getProperty('timeline', 'true'),

            browser               : System.getProperty('browser', 'chrome'),
            headless              : System.getProperty('headless', 'false'),
            webFeed               : System.getProperty('webFeed', 'transferGoWeb'),

            //mobile
            mobileFeed            : System.getProperty('mobileFeed', 'calculator'),
            hub                   : System.getProperty('hub', 'browserstack'),
            target                : System.getProperty('target', 'android'),
            runMode               : System.getProperty('runMode', 'local'),

            //Report
            slackNotif            : System.getProperty('slackNotif', 'false'), //Global => On Failures
            slackNotifyMeEverytime: System.getProperty('slackNotifyMeEverytime', 'false')
    ]
}

task runTransferGoWebTests(type: Test) {
    outputs.upToDateWhen { false }
    useTestNG {
        parallel = "methods"
        threadCount = 2
        includeGroups System.getProperty("tags", "transferGoWeb")
        testLogging.showStandardStreams = true
        useDefaultListeners true
        listeners << 'com.testvagrant.ekam.web.listeners.WebDriverListener'
        listeners << 'com.testvagrant.ekam.integrations.slack.SlackListener'
        listeners << 'com.testvagrant.ekam.commons.listeners.ReportListener'
        listeners << 'com.testvagrant.ekam.commons.listeners.OptimusDashboardListener'
        outputDirectory = file("$buildDir/" + System.getProperty('tags', 'NONE'))
    }
}

task runMobileTests(type: Test) {
    outputs.upToDateWhen { false }
    useTestNG {
        parallel = "methods"
        threadCount = 1
        includeGroups System.getProperty("tags", "mobile")
        testLogging.showStandardStreams = true
        useDefaultListeners true
        listeners << 'com.testvagrant.ekam.mobile.listeners.MobileDriverListener'
        listeners << 'com.testvagrant.ekam.commons.listeners.ReportListener'
        listeners << 'com.testvagrant.ekam.integrations.slack.SlackListener'
        listeners << 'com.testvagrant.ekam.commons.listeners.OptimusDashboardListener'
        outputDirectory = file("$buildDir/" + System.getProperty('tags', 'NONE'))
    }
}

task runApiTests(type: Test) {
    outputs.upToDateWhen { false }
    useTestNG {
        parallel = "classes"
        threadCount = 1
        includeGroups System.getProperty("tags", "api")
        testLogging.showStandardStreams = true
        useDefaultListeners true
        listeners << 'com.testvagrant.ekam.integrations.slack.SlackListener'
        outputDirectory = file("$buildDir/" + System.getProperty('tags', 'NONE'))
    }
}

allure {
    version = '2.8.1'
    autoconfigure = true
    aspectjweaver = true
    allureJavaVersion = '2.10.0'
    reportDir = new File(project.projectDir as File, '/allure-report/ui')
    resultsDir = new File(project.projectDir as File, '/allure-report/results')
    clean = true
}


task createReportDir() {
    doLast {
        def file = new File(project.projectDir as File, '/allure-report/ui')
        if (!file.exists()) {
            file.mkdirs();
        }
    }
}

task saveHistory(type: Copy) {
    from new File(project.projectDir as File, '/allure-report/ui/history')
    into new File(project.projectDir as File, '/allure-report/results/history')
}



clean {
    if (!System.getenv('CI')) {
        delete "$projectDir/allure-report"
    }
}

// Execution Rules
tasks.runTransferGoWebTests.dependsOn 'clean', 'build', 'createReportDir'
tasks.runMobileTests.dependsOn 'clean', 'build', 'createReportDir'
tasks.runApiTests.dependsOn 'clean', 'build', 'createReportDir'
tasks.build.mustRunAfter 'clean'
tasks.runTransferGoWebTests.finalizedBy('allureReport')
tasks.runMobileTests.finalizedBy('allureReport')
tasks.runApiTests.finalizedBy('allureReport')
